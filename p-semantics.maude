in "p-syntax.maude"

mod P-SEMANTICS is including P-SYNTAX .
  --- State
  -----------------

  --- The state consists of a AC soup of attributes.

  sorts Attribute Configuration .
  subsort Attribute < Configuration .
  op .Attribute : -> Configuration .
  op __ : Configuration Configuration -> Configuration [prec 65 assoc comm id: .Attribute] .

  --- "init" constructs a initial configuration from a program
  --- with the (pgm: ) record containing the input program.

  op init : Stmt -> Configuration .
  -----------------------
  eq init(Ss)
   = ( pgm: Ss )
     ( machines:  .MachineSchemas )
   .

  op (( pgm: _ )) : Stmt -> Attribute [prec 64 format(n d ++i -- d)] .

 --- Machine Schemas
 ----------------------

 --- Next, the various program declarations are loaded into the configuration

 --- Machine statements are schema for initializing new machines.
 --- For each machine statement, we load them into the ( machine: ) record
 --- via the "statementToMachineSchema" construct.

  op (( machines: _ )) : MachineSchemas -> Attribute [prec 64 format (n d ++ni d d)] .
  rl ( pgm: ( machine Id ; B ) Ss )
     (machines:  Schemas )
  => ( pgm: Ss )
     (machines:  statementToMachineSchema(machine Id ; B) ;; Schemas )
   .

  sort MachineSchema .
  op (( machine: _ , _ )) : Id MachineAttributes -> MachineSchema [format(d d d ++ni d d --ni)] .
  sort MachineAttributes .
  op .MAttrs : -> MachineAttributes .
  op _ , _ : MachineAttributes MachineAttributes -> MachineAttributes [prec 65 assoc comm id: .MAttrs format(d ni d d)] .
  var MAttrs : MachineAttributes .
  var MAttrs* : [MachineAttributes] .
  op (( start:     _ )) : Id          -> MachineAttributes  .
  op (( states:    _ )) : States      -> MachineAttributes [format(d d ++ni --ni n)] .
  op (( initState: _ )) : VariableMap -> MachineAttributes [format(d d ++ni --ni d)] .

  op noid : -> [Id] .

  sort MachineSchemas .
  subsort MachineSchema < MachineSchemas .
  op .MachineSchemas : -> MachineSchemas .
  op _ ;; _ : MachineSchemas MachineSchemas -> MachineSchemas [assoc comm id: .MachineSchemas] .

 --- For now, we we do not process "sends" clauses, so we ignore them.
  eq machine Id sends Exps ; B
   = machine Id ; B
   .

  var Schema : MachineSchema .
  var Schema* : [MachineSchema] .
  var Schemas : MachineSchemas .

  op statementToMachineSchema : MachineStmt                 -> MachineSchema .
  op statementToMachineSchema : Stmt        [MachineSchema] -> MachineSchema .
  -------------------------------------------
  eq statementToMachineSchema(machine Id ; B)
   = statementToMachineSchema(B, (machine: Id, (start: noid), (states: .States), (initState: .VariableMap))) .
  eq statementToMachineSchema({ Ss }, Schema*) = statementToMachineSchema(Ss, Schema*) .
  eq statementToMachineSchema(var X : T ; Ss, (machine: Id, (initState: Rho          ), MAttrs*))
   = statementToMachineSchema(Ss,             (machine: Id, (initState: (X |-> 0 Rho)), MAttrs*))
   .
  eq statementToMachineSchema((start state SId B) Ss, (machine: MId, (start: noid), MAttrs))
   = statementToMachineSchema((      state SId B) Ss, (machine: MId, (start: SId),  MAttrs))
   .
  eq statementToMachineSchema((state SId B) Ss, (machine: MId, (states:                                                                   States), MAttrs*))
   = statementToMachineSchema(              Ss, (machine: MId, (states: (statementToState(B, (state: SId, (actions: .Actions), .SAttrs))) States), MAttrs*))
   .
  eq statementToMachineSchema(.Stmt, Schema) = Schema .

  op statementToState(_ , _) : Stmt State -> State .
 ---------------------------------------------------
  eq statementToState({ Ss }, State) = statementToState(Ss,     State) .
  eq statementToState(entry ( Exps ) B Ss, (state: SId,                    SAttrs))
   = statementToState(Ss                 , (state: SId, (entry: Exps , B), SAttrs))
   .
  eq statementToState(on EId do ( Exps ) B Ss, (state: SId, (actions:                        Actions), SAttrs))
   = statementToState(Ss,                      (state: SId, (actions: (action: EId, Exps, B) Actions), SAttrs))
   .
  eq statementToState(.Stmt, State) = State .

 --- States
 ----------

 --- The (state: ) records store the entry, and actions corresponding to each state.

  sort State .
  sort StateAttributes .
  op .SAttrs : -> StateAttributes .
  op _ , _ : StateAttributes StateAttributes -> StateAttributes [prec 65 assoc comm id: .SAttrs] .
  var SAttrs : StateAttributes .
  --- Arguments Args Code
  op (( entry: _, _ )) : Exps Block -> StateAttributes .
  op (( actions: _ )) : Actions -> StateAttributes [format(d d ++ni --ni n)] .

  op (( state: _, _ )) : Id StateAttributes -> State [format(d d d ++ni d d --ni) ] .

  sort States .
  subsort State < States .
  op .States : -> States .
  op _ _ : States States -> States [assoc comm id: .States format(d ni d)] .

 --- Actions
 -----------

  sort Action Actions .
  subsort Action < Actions .

  op .Actions : -> Actions .
  op _ _ : Actions Actions -> Actions [prec 62 assoc comm id: .Actions] .
  var Actions : Actions .

                           --- Name, Args, Code
  op (( action: _ , _ , _ )) : Id    Exps  Stmt -> Action .

 --- Main
 --------

 --- The main cell is used to keep track of the Id of the main machine,
 --- until all declarations have been loaded.

  op (( main: _ )) : Id -> Attribute [prec 64] .
  rl ( pgm: ( main machine Id ; B ) Ss )
  => ( pgm: ( machine Id ; B ) Ss )
     ( main:  Id  )
   .

 --- Finally, the schema for the main machine is used to initialize the machine soup.
  op (( counter: _ )) : Nat -> Attribute [prec 64] .
  rl ( pgm: .Stmt )
     ( main:  MId  )
  => ( counter: 1 )
     ( instances: (new: MId, 0, .Exps) )
   .

 --- Machine Instances
 ---------------------

 --- The (instance: ) records are used to store the *runtime* state of each machine instance.
 --- Each instance is initialized from a machine schema either at the begining of execution
 --- or via the "new" construct.

  sort MachineInstance MachineInstances .
  subsort MachineInstance < MachineInstances .
  op .MachineInstances : -> MachineInstances .
  op _ _ : MachineInstances MachineInstances -> MachineInstances [prec 65 assoc comm id: .MachineInstances] .

  sort InstanceAttributes .
  op .IAttrs : -> InstanceAttributes .
  op _ , _ : InstanceAttributes InstanceAttributes -> InstanceAttributes [prec 65 assoc comm id: .IAttrs] .
  var IAttrs IAttrs1  IAttrs2 : InstanceAttributes .
  op ((code:   _ )) : Stmt        -> InstanceAttributes .
  op ((variables:  _ )) : VariableMap -> InstanceAttributes .
  op ((state:  _ )) : Id -> InstanceAttributes .
  op ((mid:  _ )) : Id -> InstanceAttributes .

  op ((buffer:  _ )) : Messages -> InstanceAttributes .
  sort Messages .
  op .Messages : -> Messages .
  op _ , _ : Messages Messages -> Messages [assoc comm id: .Messages] .
  op { _ } : Exps -> Messages  .

  op (( instance: _ , _ )) : Int InstanceAttributes -> MachineInstance [prec 63] .

  var Machine  : MachineInstance .
  var Machines : MachineInstances .

  op (( instances: _ )) : MachineInstances -> Attribute [prec 64 format (ncu o++in o n--icu p)] .

 --- Expressions
 ---------------

  op eval : Exp VariableMap -> [Exp] .
 --------------------------------------
  eq eval(I, Rho) = I .
  eq eval(Bool, Rho) = Bool .
  eq eval(Id, (Id |-> Exp) Rho) = Exp .
  eq eval(E1 < E2, Rho) = eval(E1, Rho) <Int eval(E2, Rho) .
  eq eval(E1 + E2, Rho) = eval(E1, Rho) +Int eval(E2, Rho) .
  eq eval( (Id = Exp) , Rho) = ( Id = eval(Exp, Rho) ) .
  eq eval( Id . Id1 , Id |-> (Exps1, (Id1 = Exp), Exps2) Rho ) = Exp .
  ceq eval( Id . E1 , Id |-> (Exps1, (E1 = Exp), Exps2) Rho ) = eval( E1 , (E1 |-> Exp) )
    if notBool (E1 :: Id )
   .

  op evalExps : Exps VariableMap -> Exps .
 --------------------------------------
  eq evalExps((Exp, Exps), Rho) = eval(Exp, Rho), evalExps(Exps, Rho) .
  eq evalExps(.Exps, Rho) = .Exps .

 --- Statements
 --------------

 --- Control flow

 --- Blocks
  eq (instance: N , (code: {     } Ss) , IAttrs) = (instance: N , (code:     Ss) , IAttrs) .
  eq (instance: N , (code: { S1s } Ss) , IAttrs) = (instance: N , (code: S1s Ss) , IAttrs) .

 --- goto
  rl (instance: M , (code: goto Id ; Ss), (variables: Rho), IAttrs)
  => (instance: M , enterState(Id),       (variables: Rho), IAttrs)
   .

 --- Assignment
  eq (instance: M , (code: X = E1 ; Ss), (variables: X |-> E2 Rho), IAttrs )
   = (instance: M , (code:          Ss), (variables: X |-> eval(E1, X |-> E2 Rho) Rho), IAttrs )
   .

 --- If
  eq (instance: M , (code:  if (true) B1 else B2 Ss), IAttrs )
   = (instance: M , (code:  B1                   Ss), IAttrs )
   .
  eq (instance: M , (code:  if (false) B1 else B2 Ss), IAttrs )
   = (instance: M , (code:  B2                    Ss), IAttrs )
   .
 ceq (instance: M , (code:  if (Exp)            B1 else B2 Ss), (variables: Rho), IAttrs )
   = (instance: M , (code:  if (eval(Exp, Rho)) B1 else B2 Ss), (variables: Rho), IAttrs )
  if notBool(Exp :: Bool)
   .

 --- While
  eq (instance: M , (code: while (Exp) B                           Ss), IAttrs )
   = (instance: M , (code:    if (Exp) { B while (Exp) B } else {} Ss), IAttrs )
   .

 --- inter machine interaction

 --- new
  rl ( counter: N )
     (instances: ( instance: M , (code: X = new MId(Exps); Ss), IAttrs) Machines )
  => ( counter: N +Int 1 )
     (instances: ( instance: M , (code: X = N ;            Ss), IAttrs) Machines
                    (new: MId, N, Exps)
     )
   .

 --- send
 ceq ( instance: M , (code: send Exp, Id, Exps ; Ss), (variables: Rho), IAttrs1)
   = ( instance: M , (code: send eval(Exp, Rho), Id, evalExps(Exps, Rho) ; Ss), (variables: Rho), IAttrs1)
  if notBool(Exp :: Nat) .
  rl ( instance: M , (code: send N,Exps ; Ss), IAttrs1)
     ( instance: N , (buffer: Messages), IAttrs2)
  => ( instance: M , (code: Ss), IAttrs1)
     ( instance: N , (buffer: ({Exps}, Messages)), IAttrs2)
   .

 --- action handlers
  rl (instances: ( instance: M , (code: .Stmt)
                               , (buffer: { AId , Args }, Messages)
                               , (mid: MId)
                               , (state: SId)
                               , (variables: Rho)
                               , IAttrs1
                 ) Machines )
     (machines: (machine: MId, (states: (state: SId, (actions: (action: AId, Exps, Ss) Actions), SAttrs) States)
                               , MAttrs)
             ;; Schemas)
  => (instances: ( instance: M , (code: Ss)
                               , (buffer: Messages)
                               , (mid: MId)
                               , (state: SId)
                               , (variables: bindArgs(Exps | Args | Rho ) )
                               , IAttrs1
                 ) Machines )
     (machines: (machine: MId, (states: (state: SId, (actions: (action: AId, Exps, Ss) Actions), SAttrs) States)
                               , MAttrs)
             ;; Schemas)
   .

  op bindArgs(_ | _ | _ ) : Exp Exps VariableMap -> VariableMap .
  eq bindArgs( .Exps | Exps | Rho ) = Rho . --- Todo: Check if multiple arguments are allowed
  eq bindArgs(Id : T | Exps1 | (Id |-> Exps2) Rho ) = (Id |-> Exps1) Rho .
  eq bindArgs(Id : T | Exps1 |                Rho ) = (Id |-> Exps1) Rho [owise] .

 --- arguments: MachineId, Identifier, Arguments
  op ((new: _, _, _)) : Id Nat Exps -> MachineInstance .
  eq (instances: (new: MId, N, Exps) Machines )
     (machines:  (machine: MId, (start: StartId), (initState: Rho), MAttrs) ;; Schemas )
   = (machines:  (machine: MId, (start: StartId), (initState: Rho), MAttrs) ;; Schemas )
     (instances: (instance: N, (mid: MId)
                             , enterState(StartId)
                             , (state: noid)
                             , (variables: (this |-> N) Rho)
                             , (buffer: .Messages)
                 )
                 Machines
     )
   .

  op enterState : Id -> InstanceAttributes .
  eq (instances: (instance: M , (mid: MId), (state: Id*), enterState(SId), IAttrs) Machines)
     (machines:  (machine: MId, (states: (state: SId, SAttrs) States), MAttrs) ;; Schemas )
   = (instances: (instance: M , (mid: MId), (state: SId), enterState.code(SId), IAttrs) Machines)
     (machines:  (machine: MId, (states: (state: SId, SAttrs) States), MAttrs) ;; Schemas )
   .

  op enterState.code : Id -> InstanceAttributes .
  eq (instances: (instance: M , (mid: MId), enterState.code(SId), IAttrs) Machines)
     (machines:  (machine: MId, (states: (state: SId, (entry: Exps , Ss), SAttrs) States), MAttrs) ;; Schemas )
   = (instances: (instance: M , (mid: MId), (code: Ss), IAttrs) Machines)
     (machines:  (machine: MId, (states: (state: SId, (entry: Exps , Ss), SAttrs) States), MAttrs) ;; Schemas )
   .
  eq (instances: (instance: M , (mid: MId), enterState.code(SId), IAttrs) Machines)
     (machines:  (machine: MId, (states: (state: SId, SAttrs) States), MAttrs) ;; Schemas )
   = (instances: (instance: M , (mid: MId), (code: .Stmt), IAttrs) Machines)
     (machines:  (machine: MId, (states: (state: SId, SAttrs) States), MAttrs) ;; Schemas )
     [owise]
   .

  --- Other infrastructure
  ------------------------

  --- Macros: Add empty arguments to entry/actions without arguments
  eq entry B = entry (.Exps) B .
  eq on EId do B = on EId do (.Exps) B .

  --- Maps
  sort VariableMap .
  op .VariableMap : -> VariableMap .
  op _|->_ : Id Exps -> VariableMap [prec 60 format (n++i r o --i) ] .
  op _ _ : VariableMap VariableMap -> VariableMap [prec 62 assoc comm id: .VariableMap] .

  var X Y Id MId SId EId AId Id1 Id2 : Id .
  var N M : Nat .
  var I I1 I2 : Int .
  var Bool Bool1 Bool2 : Bool .
  var StartId  : Id .
  var Id* StartId* : [Id] .
  var T : Type .
  var B B1 B2 : Block .
  var Ss S1s S2s : Stmt .
  var Rho : VariableMap .
  var C : Configuration .
  var Args Exps Exps1 Exps2 : Exps .
  var Exp E1 E2 : Exp .
  var State : State .
  var States : States .
  var Messages : Messages .

endm

